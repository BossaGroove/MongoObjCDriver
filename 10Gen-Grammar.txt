/*
    A Grammar for JSON.
*/

@start        = Empty | arrayElement | objectElement;

objectElement        = openCurlyToken objectContentElement closeCurlyToken;
objectContentElement = Empty | actualObjectElement;
actualObjectElement  = propertyElement commaPropertyElement*;
propertyElement      = propertyNameElement colonToken valueElement;
commaPropertyElement = commaToken propertyElement;
propertyNameElement  = quotedStringToken | wordToken;         // NOTE: property names are quoted in JSON

arrayElement         = openBracketToken arrayContentElement closeBracketToken;
arrayContentElement  = Empty | actualArrayElement;
actualArrayElement   = valueElement commaValueElement*;
commaValueElement    = commaToken valueElement;

valueElement         = nullToken | trueToken | falseToken | arrayElement | objectElement | numberToken | quotedStringToken | undefinedToken | objectIDElement;

objectIDElement      = objectIDToken openParentheseToken wordToken closeParenthesetoken;

quotedStringToken    = QuotedString;
wordToken            = Word;
numberToken          = Number;
nullToken            = 'null';
trueToken            = 'true';
falseToken           = 'false';
undefinedToken       = 'undefined';
objectIDToken        = 'ObjectID';

openCurlyToken       = '{';
closeCurlyToken      = '}';
openBracketToken     = '[';
closeBracketToken    = ']';
commaToken           = ',';
colonToken           = ':';
openParentheseToken  = '(';
closeParenthesetoken = ')';
